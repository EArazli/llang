doctrine CCC_Bool where {
  sort Obj;
  sort Hom (a:Obj) (b:Obj);

  op id : (x:Obj) -> Hom(?x, ?x);

  op comp :
    (a:Obj) (b:Obj) (c:Obj)
    (f:Hom(?b, ?c)) (g:Hom(?a, ?b))
    -> Hom(?a, ?c);

  computational idL :
    (a:Obj) (b:Obj) (g:Hom(?a, ?b)) |-
      comp(?a, ?b, ?b, id(?b), ?g) -> ?g;

  computational idR :
    (a:Obj) (b:Obj) (f:Hom(?a, ?b)) |-
      comp(?a, ?a, ?b, ?f, id(?a)) -> ?f;

  structural assoc :
    (a:Obj) (b:Obj) (c:Obj) (d:Obj)
    (h:Hom(?c, ?d)) (g:Hom(?b, ?c)) (f:Hom(?a, ?b)) |-
      comp(?a, ?c, ?d, ?h, comp(?a, ?b, ?c, ?g, ?f))
        = comp(?a, ?b, ?d, comp(?b, ?c, ?d, ?h, ?g), ?f);

  op Unit : Obj;
  op prod : (a:Obj) (b:Obj) -> Obj;

  op exl : (a:Obj) (b:Obj) -> Hom(prod(?a, ?b), ?a);
  op exr : (a:Obj) (b:Obj) -> Hom(prod(?a, ?b), ?b);

  op pair :
    (a:Obj) (b:Obj) (c:Obj)
    (f:Hom(?c, ?a)) (g:Hom(?c, ?b))
    -> Hom(?c, prod(?a, ?b));

  computational exl_pair :
    (a:Obj) (b:Obj) (c:Obj)
    (f:Hom(?c, ?a)) (g:Hom(?c, ?b)) |-
      comp(?c, prod(?a, ?b), ?a,
           exl(?a, ?b),
           pair(?a, ?b, ?c, ?f, ?g))
        -> ?f;

  computational exr_pair :
    (a:Obj) (b:Obj) (c:Obj)
    (f:Hom(?c, ?a)) (g:Hom(?c, ?b)) |-
      comp(?c, prod(?a, ?b), ?b,
           exr(?a, ?b),
           pair(?a, ?b, ?c, ?f, ?g))
        -> ?g;

  op exp : (a:Obj) (b:Obj) -> Obj;

  op curry :
    (a:Obj) (b:Obj) (c:Obj)
    (f:Hom(prod(?c, ?a), ?b))
    -> Hom(?c, exp(?a, ?b));

  op eval :
    (a:Obj) (b:Obj)
    -> Hom(prod(exp(?a, ?b), ?a), ?b);

  op terminal : (a:Obj) -> Hom(?a, Unit);

  computational terminal_unit :
    terminal(Unit) -> id(Unit);

  computational beta :
    (a:Obj) (b:Obj) (c:Obj)
    (f:Hom(prod(?c, ?a), ?b)) |-
      comp(prod(?c, ?a), prod(exp(?a, ?b), ?a), ?b,
           eval(?a, ?b),
           pair(exp(?a, ?b), ?a, prod(?c, ?a),
                comp(prod(?c, ?a), ?c, exp(?a, ?b),
                     curry(?a, ?b, ?c, ?f),
                     exl(?c, ?a)),
                exr(?c, ?a)))
        -> ?f;

  computational beta_app :
    (a:Obj) (b:Obj) (c:Obj)
    (f:Hom(prod(?c, ?a), ?b)) (g:Hom(?c, ?a)) |-
      comp(?c, prod(exp(?a, ?b), ?a), ?b,
           eval(?a, ?b),
           pair(exp(?a, ?b), ?a, ?c,
                curry(?a, ?b, ?c, ?f),
                ?g))
        -> comp(?c, prod(?c, ?a), ?b,
                ?f,
                pair(?c, ?a, ?c, id(?c), ?g));

  op Bool : Obj;
  op T : Hom(Unit, Bool);
  op F : Hom(Unit, Bool);
}

doctrine CCC extends CCC_Bool where {
}
